;------------------------------------------------------------------------
; RAT - Radar Tools
;------------------------------------------------------------------------
;;;	 mm_:	MATRIX MATHEMATICS routines			;;;
;;;		for fast mathematical operations		;;;
;;;		of complex matrices/vectors/scalars		;;;
;;;		over all dimensions				;;;
;;;		utilising mathematical matrix notation		;;;
;;; matrix determinant over all dimensions
;;;   Maxim Neumann - 03/2006
;;;   based on rat-block-routines by A. Reigber and S. Guillaso
;;; mm_determ(a[*,*,0])	<==>  determ(a[*,*,0])
;------------------------------------------------------------------------
; The contents of this file are subject to the Mozilla Public License
; Version 1.1 (the "License"); you may not use this file except in
; compliance with the License. You may obtain a copy of the License at
; http://www.mozilla.org/MPL/
;
; Software distributed under the License is distributed on an "AS IS"
; basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
; License for the specific language governing rights and limitations
; under the License.
;
; The Initial Developer of the Original Code is the RAT development team.
; All Rights Reserved.
;------------------------------------------------------------------------


function mm_determ, A

  siz = size(A)
  det = make_array( (siz[0]le 2? 1: siz[3:siz[0]]), $
                    TYPE=siz[siz[0]+1], /NOZERO)
  case siz[1] of
     2: det[*] = A  [0,0,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] $
              - A[1,0,*,*,*,*,*,*] * A[0,1,*,*,*,*,*,*]
     3: det[*] = A  [0,0,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              - A[0,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] $
              - A[0,1,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              + A[0,1,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] $
              + A[0,2,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] $
              - A[0,2,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*]
     4: det[*] = A  [0,0,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $ 
              - A[0,0,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              - A[0,0,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $
              + A[0,0,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              + A[0,0,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] $
              - A[0,0,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              - A[0,1,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $
              + A[0,1,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              + A[0,1,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $
              - A[0,1,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              - A[0,1,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] $
              + A[0,1,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              + A[0,2,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $
              - A[0,2,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              - A[0,2,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,3,*,*,*,*,*,*] $
              + A[0,2,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,3,*,*,*,*,*,*] $
              + A[0,2,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] $
              - A[0,2,*,*,*,*,*,*] * A[1,3,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] $
              - A[0,3,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] $
              + A[0,3,*,*,*,*,*,*] * A[1,0,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              + A[0,3,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,2,*,*,*,*,*,*] $
              - A[0,3,*,*,*,*,*,*] * A[1,1,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,2,*,*,*,*,*,*] $
              - A[0,3,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[2,0,*,*,*,*,*,*] * A[3,1,*,*,*,*,*,*] $
              + A[0,3,*,*,*,*,*,*] * A[1,2,*,*,*,*,*,*] * A[3,0,*,*,*,*,*,*] * A[2,1,*,*,*,*,*,*]
     else: $
        for n=0,(siz[0]lt 8?0:siz[8]-1) do $
           for m=0,(siz[0]lt 7?0:siz[7]-1) do $
              for l=0,(siz[0]lt 6?0:siz[6]-1) do $
                 for k=0,(siz[0]lt 5?0:siz[5]-1) do $
                    for j=0,(siz[0]lt 4?0:siz[4]-1) do $
                       for i=0,(siz[0]lt 3?0:siz[3]-1) do $
                          det[i,j,k,l,m,n] = la_determ(A[*,*,i,j,k,l,m,n],/check)
  endcase

  return, det
end
